# We use Node2D for Player, not Area2D similar to Enemy as a simplification,
# because there's no collision logic needed for Player for now.
class_name Player
extends Node2D

@export var stats: CharacterStats : set = set_character_stats

@onready var sprite_2d: Sprite2D = $Sprite2D
@onready var stats_ui: StatsUI = $StatsUI as StatsUI


# FOR TESTING
#func _ready() -> void:
	#await get_tree().create_timer(4).timeout
	#take_damage(21)
	#stats.block += 17
	

func set_character_stats(value: CharacterStats) -> void:
	stats = value
	
	# In Godot, the setter function for an exported variable gets called even when you
	# run the game. That means we could accidentally connect twice when later on we set up
	# everything in the parent Battle node. We can alternatively define it in Battle.gd.
	# We set up this way so Player can run on its own, we can test it in isolation.
	if not stats.stats_changed.is_connected(update_stats):
		stats.stats_changed.connect(update_stats)

	update_player()


func update_player() -> void:
	if not stats is CharacterStats: 
		return
	# Because of @export, we could call the function sooner than the node is ready
	if not is_inside_tree(): 
		await ready

	sprite_2d.texture = stats.art
	update_stats()
	
	
func update_stats() -> void:
	stats_ui.update_stats(stats)


func take_damage(damage: int) -> void:
	if stats.health <= 0:
		return
		
	stats.take_damage(damage)
	
	if stats.health <= 0:
		Events.player_died.emit()
		# Delete player from the scene
		queue_free()
